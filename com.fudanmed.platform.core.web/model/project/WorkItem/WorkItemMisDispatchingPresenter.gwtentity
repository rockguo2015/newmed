package com.fudanmed.platform.core.web feature project{
	
	import java.util.*
	import edu.fudan.langlab.uidl.domain.app.*
	import com.google.gwt.user.client.^ui.*	
	import com.google.gwt.editor.client.HasEditorErrors
	import edu.fudan.langlab.domain.organization.*
	import edu.fudan.langlab.domain.security.*
	import edu.fudan.langlab.uidl.domain.app.client.web.*
	import com.uniquesoft.gwt.client.common.*
	import com.uniquesoft.gwt.client.common.widgets.*
	import com.fudanmed.platform.core.web.*
	 
	import com.fudanmed.platform.core.common.*
	import com.fudanmed.platform.core.domain.*
	
	ui-datatype WorkItemMisDispatchingData{
		finishDate : Date "完工[串单]日期"
		finishTime : Date "完工[串单]时间"
		comment : String "维修说明"
		alsoReport : Boolean "串单上报"
		reportComment : String "上报信息"
		
		constraints WorkItemMisDispatchingDataValidator{
			rule Required on finishDate
			rule Required on finishTime
		}
	
		ui-component WorkItemMisDispatchingDataForm as GXTForm{
			binding{ finishDate, finishTime as TimeItem, comment as TextAreaItem, alsoReport as CheckBoxItem, reportComment as TextAreaItem}
			con : Widget
			op asWidget() : Widget{
				con = con.lazy[
					widgets.VLayout=>[
						add(finishDate.asWidget,widgets.VLayoutData(1,-1))
						add(finishTime.asWidget,widgets.VLayoutData(1,-1))
						add(comment.asWidget,widgets.VLayoutData(366,80))
						add(alsoReport.asWidget,widgets.VLayoutData(120,-1))
						add(reportComment.asWidget,widgets.VLayoutData(366,80))
					]
				]
			}			
		}
	}
	
	presenter WorkItemMisDispatchingPresenter  with-validation implements CommitablePresenter{
		view interface{
			op getValue() : WorkItemMisDispatchingData
			signal in setComment(comment : String)
			signal in canReport()
		}
		
		interaction{
			context : proxy of RCWorkItemTask
			input call.setup(context : proxy of RCWorkItemTask,postInit : (WorkItemMisDispatchingPresenter)=>void){
				this.context = context
				this.activate[
					service.getComment(context,onSuccess[
						fire view.setComment(it)
						service.canReport(context,onSuccess[canReport|
							if(canReport) fire view.canReport()
							postInit.apply(this)
						])
					])
				]
			}
			
			input call.performCommit(CommitResultNotifier notifier) {
				service.commit(context,getView().value,onSuccess[
					fire eventbus.WorkItemTasksChanged
					fire eventbus.GroupTasksChanged
					notifier.success
				])
			}
		}
	
		service{
			@Inject ^validator : WorkItemMisDispatchingDataValidator
			op commit(context : proxy of RCWorkItemTask,data : WorkItemMisDispatchingData) : void{
				data.validateWith(^validator)
				val value = (resolve context)
				value.finishDate = data.finishDate
				value.finishTime = data.finishTime
				value.comment = data.comment
				value.misDispatching
				
				//TODO status check
				if(data.alsoReport){
					value.groupTask.comment = data.reportComment
					value.groupTask.finishDate = data.finishDate
					value.groupTask.finishTime = data.finishTime
					
					value.groupTask.reportMisdispatch
				}
			}
			op getComment(context : proxy of RCWorkItemTask):String{
				(resolve context).comment
			}
			op canReport(context: proxy of RCWorkItemTask):Boolean{
				val task = resolve context
				task.groupTask.allTasksFinishedExcept(task)
			}
		}
	}

	view WorkItemMisDispatchingView for WorkItemMisDispatchingPresenter  implements IHasSize, IHasTitle{
		@Inject form : WorkItemMisDispatchingDataForm
		required timeService : client.common.ServerSideTimeServiceAsync
	
		initializer{
			this +=  widgets.VLayout=>[add( form.asWidget=>[
				timeService.getTime(onSuccess[
					form.finishDate.value = it.trimDate
					form.finishTime.value = it
				])
					form.alsoReport.asWidget=>[disable]
					form.reportComment.asWidget=>[disable]
				],widgets.VLayoutData(1,-1,10))					
			]
		}
		
		op getValue() : WorkItemMisDispatchingData{
			form.value
		}
		
		op mapField(String errorKey): HasEditorErrors {
			form.mapToErrorEditor(errorKey)
		}
		op clearErrors():void{   
			form.clearErrors
		}
		
		op getSize():Size{
			new Size(400,360)
		}
		op getTitle():String{
			"工单串单"
		}
		op setComment(comment : String):void{
			form.comment.value = comment
		}
		op canReport():void{
			form.alsoReport.asWidget=>[
				form.alsoReport.value = true
				enable
			]
			form.reportComment.asWidget=>[enable]
		}
	}

}